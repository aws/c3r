plugins {
    // Java compilation, unit tests, and library distribution needs. Read more at:
    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    id 'java-library'

    // SpotBugs for quality checks and reports of source files. Read more at:
    // https://spotbugs.readthedocs.io/en/stable/gradle.html
    id "com.github.spotbugs" version "5.1.1"
}

apply plugin: 'com.github.spotbugs'

/*
 Configures the SpotBugs "com.github.spotbugs" plugin. Remove this and the
 plugin to skip these checks and report generation.
*/
spotbugs {
    ignoreFailures.set(false)
    spotbugsTest.enabled = false
    excludeFilter.set(file("../config/spotbugs/excludeFilter.xml"))
}

dependencies {
    // AWS-C3R
    implementation project(":c3r-sdk-core")
    implementation project(":c3r-sdk-parquet")

    // Spark
    implementation "org.apache.spark:spark-core_2.13:$spark_version"
    implementation "org.apache.spark:spark-streaming_2.13:$spark_version"
    implementation "org.apache.spark:spark-sql_2.13:$spark_version"
}

test {
    useJUnitPlatform()

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        showStandardStreams = false
    }

    // Required for Spark. While Spark supports Java 17, it still makes a call to this class
    // and will cause a runtime failure.
    jvmArgs '--add-exports=java.base/sun.nio.ch=ALL-UNNAMED'
}
